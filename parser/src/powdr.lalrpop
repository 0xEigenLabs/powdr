use std::str::FromStr;
use crate::ast::*;
use crate::asm_ast::*;
use number::{AbstractNumberType, FieldElement};
use num_traits::Num;

grammar<T> where T: FieldElement;

match {
    r"\s*" => { },
    r"//[^\n\r]*[\n\r]*" => { }, // Skip `// comments`
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => { },  // Skip `/* comments */`
    _,
}

pub PILFile: PILFile<T> = {
    (<Statement> ";")* => PILFile(<>)

};

pub ASMFile: ASMFile<T> = {
    (<ASMStatement>)* => ASMFile(<>)
};

// ---------------------------- PIL part -----------------------------

Statement = {
    Include,
    Namespace,
    ConstantDefinition,
    PolynomialDefinition,
    PublicDeclaration,
    PolynomialConstantDeclaration,
    PolynomialConstantDefinition,
    PolynomialCommitDeclaration,
    PolynomialIdentity,
    PlookupIdentity,
    PermutationIdentity,
    ConnectIdentity,
    MacroDefinition,
    FunctionCallStatement,
    ASMBlock,
};

Include: Statement<T> = {
    <start:@L> "include" <file:StringLiteral> => Statement::Include(<>)
};

Namespace: Statement<T> = {
    <start:@L> "namespace" <name:Identifier> "(" <pol_degree:Expression> ")" => Statement::Namespace(<>)
}

ConstantDefinition: Statement<T> = {
    <@L> "constant" <ConstantIdentifier> "=" <Expression> => Statement::ConstantDefinition(<>)
}

PolynomialDefinition: Statement<T> = {
    <@L> PolCol <Identifier> "=" <Expression> => Statement::PolynomialDefinition(<>)
}

PublicDeclaration: Statement<T> = {
    <@L> "public" <Identifier> "=" <PolynomialReference> "(" <Expression> ")" => Statement::PublicDeclaration(<>)
}

PolynomialConstantDeclaration: Statement<T> = {
    <@L> PolCol ConstantFixed <PolynomialNameList> => Statement::PolynomialConstantDeclaration(<>)
}

PolynomialConstantDefinition: Statement<T> = {
    <@L> PolCol ConstantFixed <Identifier> <FunctionDefinition>
        => Statement::PolynomialConstantDefinition(<>)
}

FunctionDefinition: FunctionDefinition<T> = {
    "(" <ParameterList> ")" "{" <Expression> "}" => FunctionDefinition::Mapping(<>),
    "=" <ArrayLiteralExpression> => FunctionDefinition::Array(<>),
}

ParameterList: Vec<String> = {
    <mut list:( <Identifier> "," )*> <end:Identifier>  => { list.push(end); list }
}

ArrayLiteralExpression: ArrayExpression<T> = {
    <ArrayLiteralExpression> "+" <ArrayLiteralTerm> => ArrayExpression::concat(<>),
    ArrayLiteralTerm,
}

ArrayLiteralTerm: ArrayExpression<T> = {
    "[" <ExpressionList> "]" => ArrayExpression::value(<>),
    "[" <ExpressionList> "]" "*" => ArrayExpression::repeated_value(<>),
}

PolynomialCommitDeclaration: Statement<T> = {
    <@L> PolCol CommitWitness <PolynomialNameList> => Statement::PolynomialCommitDeclaration(<>, None),
    <start:@L> PolCol CommitWitness <name:PolynomialName> "(" <param:ParameterList> ")" "query" <value:Expression>
     => Statement::PolynomialCommitDeclaration(start, vec![name], Some(FunctionDefinition::Query(param, value)))
}

PolynomialIdentity: Statement<T> = {
    <start:@L> <l:BoxedExpression> "=" <r:BoxedExpression> => Statement::PolynomialIdentity(start, Expression::BinaryOperation(l, BinaryOperator::Sub, r))
}

PolynomialNameList: Vec<PolynomialName<T>> = {
    <mut list:( <PolynomialName> "," )*> <end:PolynomialName>  => { list.push(end); list }
}

PolynomialName: PolynomialName<T> = {
    <name:Identifier> <array_size:( "[" <Expression> "]" )?> => PolynomialName{<>}
}

PlookupIdentity: Statement<T> = {
    <@L> <SelectedExpressions> "in" <SelectedExpressions> => Statement::PlookupIdentity(<>)
}

SelectedExpressions: SelectedExpressions<T> = {
    <selector:Expression?> "{" <expressions:ExpressionList> "}" => SelectedExpressions{<>},
    Expression => SelectedExpressions{selector: None, expressions: vec![<>]},
}

PermutationIdentity: Statement<T> = {
    <@L> <SelectedExpressions> "is" <SelectedExpressions> => Statement::PermutationIdentity(<>)
}

ConnectIdentity: Statement<T> = {
    <@L> "{" <ExpressionList> "}" "connect" "{" <ExpressionList> "}" => Statement::ConnectIdentity(<>)
}

MacroDefinition: Statement<T> = {
    <@L> "macro" <Identifier> "(" <ParameterList> ")" "{" <( <Statement> ";")*> <Expression?> "}"
        => Statement::MacroDefinition(<>)
}

FunctionCallStatement: Statement<T> = {
    <@L> <Identifier> "(" <ExpressionList> ")" => Statement::FunctionCall(<>)
}

ASMBlock: Statement<T> = {
    <@L> "assembly" "{" <(ASMStatement)*> "}" => Statement::ASMBlock(<>)
}

PolCol = {
    "pol", "col"
} 

CommitWitness = {
    "commit", "witness"
} 

ConstantFixed = {
    "constant", "fixed"
} 

// ---------------------------- ASM part -----------------------------

ASMStatement: ASMStatement<T> = {
    Degree,
    RegisterDeclaration,
    InstructionDeclaration,
    InlinePil,
    Assignment,
    Instruction,
    Label,
}

Degree: ASMStatement<T> = {
    <@L> "degree" <Integer> ";" => ASMStatement::Degree(<>)
}

RegisterDeclaration: ASMStatement<T> = {
    // TODO default update
    <@L> "reg" <Identifier> <( "[" <RegisterFlag> "]" )?> ";" => ASMStatement::RegisterDeclaration(<>)

}

RegisterFlag: RegisterFlag = {
    "@pc" => RegisterFlag::IsPC,
    "<=" => RegisterFlag::IsAssignment
}

InstructionDeclaration: ASMStatement<T> = {
    <@L> "instr" <Identifier> <InstructionParams> "{" <InstructionBodyElements> "}" => ASMStatement::InstructionDeclaration(<>)
}

InstructionBodyElements: Vec<InstructionBodyElement<T>> = {
    <mut list:( <InstructionBodyElement> "," )*> <end:InstructionBodyElement>  => { list.push(end); list }
}

InstructionBodyElement: InstructionBodyElement<T> = {
    <l:BoxedExpression> "=" <r:BoxedExpression> => InstructionBodyElement::Expression(Expression::BinaryOperation(l, BinaryOperator::Sub, r)),
    <SelectedExpressions> <PlookupOperator> <SelectedExpressions> => InstructionBodyElement::PlookupIdentity(<>),
}

// This is only valid in instructions, not in PIL in general.
// "connect" is not supported because it does not support selectors
// and we need that for the instruction.

PlookupOperator: PlookupOperator = {
    "in" => PlookupOperator::In,
    "is" => PlookupOperator::Is,
}

InstructionParams: InstructionParams = {
    <_input: InstructionParamList> "->" <output: InstructionParamList> => InstructionParams::new(_input, Some(output)),
    // we can ommit the arrow if there are no outputs
    <_input: InstructionParamList> => InstructionParams::new(_input, None)

}

InstructionParamList: InstructionParamList = {
    => InstructionParamList::new(vec![]),
    <mut list:( <InstructionParam> "," )*> <end:InstructionParam>  => { list.push(end); InstructionParamList::new(list) }
}

InstructionParam: InstructionParam = {
    <name: Identifier> <ty:(":" <Identifier>)?> =>
        InstructionParam{name, ty}
}

InlinePil: ASMStatement<T> = {
    <@L> "pil" "{" <(<Statement> ";")*> "}" => ASMStatement::InlinePil(<>)
}

Assignment: ASMStatement<T> = {
    <@L> <IdentifierList> <AssignOperator> <BoxedExpression> ";" => ASMStatement::Assignment(<>)

}

IdentifierList: Vec<String> = {
    <mut list:( <Identifier> "," )*> <end:Identifier>  => { list.push(end); list }
}

AssignOperator: Option<String> = {
    "<=" <Identifier?> "="
}

Instruction: ASMStatement<T> = {
    <@L> <Identifier> <ExpressionList> ";" => ASMStatement::Instruction(<>)
}

Label: ASMStatement<T> = {
    <@L> <Identifier> "::" => ASMStatement::Label(<>)
}

// ---------------------------- Expressions -----------------------------

ExpressionList: Vec<Expression<T>> = {
    => vec![],
    <mut list:( <Expression> "," )*> <end:Expression>  => { list.push(end); list }
}

Expression: Expression<T> = {
    BoxedExpression => *<>,
}

BoxedExpression: Box<Expression<T>> = {
    BinaryOr,
}

BinaryOr: Box<Expression<T>> = {
    BinaryOr BinaryOrOp BinaryXor => Box::new(Expression::BinaryOperation(<>)),
    BinaryXor,
}

BinaryOrOp: BinaryOperator = {
    "|" => BinaryOperator::BinaryOr,
}

BinaryXor: Box<Expression<T>> = {
    BinaryXor BinaryXorOp BinaryAnd => Box::new(Expression::BinaryOperation(<>)),
    BinaryAnd,
}

BinaryXorOp: BinaryOperator = {
    "^" => BinaryOperator::BinaryXor,
}

BinaryAnd: Box<Expression<T>> = {
    BinaryAnd BinaryAndOp BitShift => Box::new(Expression::BinaryOperation(<>)),
    BitShift,
}

BinaryAndOp: BinaryOperator = {
    "&" => BinaryOperator::BinaryAnd,
}

BitShift: Box<Expression<T>> = {
    BitShift BitShiftOp Sum => Box::new(Expression::BinaryOperation(<>)),
    Sum,
}

BitShiftOp: BinaryOperator = {
    "<<" => BinaryOperator::ShiftLeft,
    ">>" => BinaryOperator::ShiftRight,
}

Sum: Box<Expression<T>> = {
    Sum SumOp Product => Box::new(Expression::BinaryOperation(<>)),
    Product,
}

SumOp: BinaryOperator = {
    "+" => BinaryOperator::Add,
    "-" => BinaryOperator::Sub,
}

Product: Box<Expression<T>> = {
    Product ProductOp Power => Box::new(Expression::BinaryOperation(<>)),
    Power,
}

ProductOp: BinaryOperator = {
    "*" => BinaryOperator::Mul,
    "/" => BinaryOperator::Div,
    "%" => BinaryOperator::Mod,
}

Power: Box<Expression<T>> = {
    <Power> <PowOp> <Term> => Box::new(Expression::BinaryOperation(<>)),
    Unary,
}

PowOp: BinaryOperator = {
    "**" => BinaryOperator::Pow,
}

Unary: Box<Expression<T>> = {
    UnaryOp Term => Box::new(Expression::UnaryOperation(<>)),
    Term,
}

UnaryOp: UnaryOperator = {
    "+" => UnaryOperator::Plus,
    "-" => UnaryOperator::Minus,
}

Term: Box<Expression<T>> = {
    FunctionCall => Box::new(<>),
    ConstantIdentifier => Box::new(Expression::Constant(<>)),
    PolynomialReference => Box::new(Expression::PolynomialReference(<>)),
    PublicReference => Box::new(Expression::PublicReference(<>)),
    FieldElement => Box::new(Expression::Number(<>)),
    StringLiteral => Box::new(Expression::String(<>)),
    MatchExpression,
    "(" <head:Expression> "," <tail:ExpressionList> ")" => { let mut list = vec![head]; list.extend(tail); Box::new(Expression::Tuple(list)) },
    "(" <BoxedExpression> ")",
    "${" <BoxedExpression> "}" => Box::new(Expression::FreeInput(<>))
}

FunctionCall: Expression<T> = {
    <Identifier> "(" <ExpressionList> ")" => Expression::FunctionCall(<>)
}

PolynomialReference: PolynomialReference<T> = {
    <namespace:( <Identifier> "." )?>
    <name:Identifier>
    <index:( "[" <BoxedExpression> "]" )?>
    <next:"'"?> => PolynomialReference{namespace, name, index, next: next.is_some()},
}

PublicReference: String = {
    ":" <Identifier>
}

MatchExpression: Box<Expression<T>> = {
    "match" <BoxedExpression> "{" <MatchArms> "}" => Box::new(Expression::MatchExpression(<>))
}

MatchArms: Vec<(Option<Expression<T>>, Expression<T>)> = {
    => vec![],
    <mut list:( <MatchArm> "," )*> <end:MatchArm> ","?  => { list.push(end); list }
}

MatchArm: (Option<Expression<T>>, Expression<T>) = {
    <n:Expression> "=>" <e:Expression> => (Some(n), e),
    <n:"_"> "=>" <e:Expression> => (None, e),
}

// ---------------------------- Terminals -----------------------------


StringLiteral: String = {
    r#""[^"]*""# => <>[1..<>.len() - 1].to_string()
}

Identifier: String = {
    r"[a-zA-Z_][a-zA-Z$_0-9@]*" => <>.to_string()
}

ConstantIdentifier: String = {
    // TODO it seems the lexer splits the token after %
    "%N" => <>.to_string(),
    r"%[a-zA-Z_][a-zA-Z$_0-9@]*" => <>.to_string(),
}

FieldElement: T = {
    r"[0-9][0-9_]*" => T::from_str(&<>.replace('_', "")),
    r"0x[0-9A-Fa-f][0-9A-Fa-f_]*" => T::from_str_radix(&<>[2..].replace('_', ""), 16).unwrap(),
}

Integer: AbstractNumberType = {
    r"[0-9][0-9_]*" => AbstractNumberType::from_str(&<>.replace('_', "")).unwrap(),
    r"0x[0-9A-Fa-f][0-9A-Fa-f_]*" => AbstractNumberType::from_str_radix(&<>[2..].replace('_', ""), 16).unwrap(),
}